
plugins {
  id 'eclipse'
  id 'maven'
  id 'java'
  id 'io.toolebox.git-versioner' version "1.3.0"
}
versioner {
  pattern {
    pattern = "%M.%m.%p(.%c)"
  }
  
  startFrom {
    major = 0
    minor = 5
    patch = 1
  }
  
  tag {
    prefix = ''
  }
}

group = 'com.github.inversion-api'
sourceCompatibility = 1.8

repositories {
   mavenLocal()
   mavenCentral()
   maven { url 'https://jitpack.io' }
}

configurations.all { resolutionStrategy.cacheChangingModulesFor 0, 'seconds' }

apply from: 'build.prj.gradle'

eclipse {
   classpath {
      file {
         tasks.eclipse.dependsOn(cleanEclipseClasspath)
         tasks.eclipse.dependsOn("create-dirs")
      }
   }
}

task "create-dirs" {
   doLast {
      sourceSets*.java.srcDirs*.each { it.mkdirs() }
      sourceSets*.resources.srcDirs*.each { it.mkdirs() }
   }
}

test {
    //test are not run for now because we can't figure out how to 
    //run tests that require live data backends and credentials
    //as part of a portable automaoted build/test process
    //there are many many test but right now, the easiest way
    //to run them is from eclipse.
    exclude '**/*'
}


task writeNewPom {
    doLast {
        pom {
            project {
                inceptionYear '2015',
                organization {
                	name 'Rocket Partners, LLC'
                	url 'https://github.com/inversion-api/' + project.name
                }
                licenses {
                    license {
                        name 'GNU AFFERO GENERAL PUBLIC LICENSE'
                        url 'https://www.gnu.org/licenses/agpl-3.0.en.html'
                        distribution 'repo'
                    }
                }
            }
        }.writeTo("pom.xml")
    }
}



    